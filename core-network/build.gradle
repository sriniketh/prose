plugins {
    alias libs.plugins.android.library
    alias libs.plugins.kotlin.android
    alias libs.plugins.hilt
    alias libs.plugins.ksp
}

def apikeyPropertiesFile = file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

kotlin {
    jvmToolchain(rootProject.ext.jvmToolchainVersion)
}

android {
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion

        buildConfigField("String", "BOOKS_API_KEY", apikeyProperties['BOOKS_API_KEY'])
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    kotlinOptions {
        freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
    }
    buildFeatures {
        buildConfig true
    }
    namespace 'com.sriniketh.prose.core_network'
}

dependencies {

    implementation libs.android.core.ktx
    implementation libs.coroutines.android

    implementation libs.okhttp.logging.interceptor
    implementation libs.retrofit
    implementation libs.retrofit.moshi.converter
    implementation libs.moshi
    ksp libs.moshi.codegen

    implementation libs.hilt.android
    ksp libs.hilt.compiler

    testImplementation libs.junit
    testImplementation libs.coroutines.test
}
